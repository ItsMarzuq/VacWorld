agent Follower {
    module EIS ei;
    module Console C;
    module Routing routing;
    module System S;
    
    types follower {
        formula task(string);
        formula waitingForTask(boolean);
        formula leader(string);
        formula requestTask();
        formula assignTask(long, long);
        formula noTask();
        formula taskComplete(long, long);
        formula targetX(long);
        formula targetY(long);
    }
    
    rule +!main([string vacbot, string leaderName]) {
        ei.join("hw");
        ei.link(vacbot);
        +leader(leaderName);
        +waitingForTask(false);
        C.println("Follower " + vacbot + " activated, leader: " + leaderName);
    }
    
    rule +$ei.event(location(long X, long Y)) {
        !update();
        
        if (targetX(long tX) & targetY(long tY) & X == tX & Y == tY) {
            !cleanTask(tX, tY);
        } else {
            !doTask();
        }
    }
    
    rule +!update() : ei.location(long X, long Y) & ei.direction(string D) {
        foreach(ei.square(string loc, "obstacle") & 
                ~routing.obstacle(X, Y, D, loc)) {
            routing.recordObstacle(X, Y, D, loc);
        }
        long maxX = routing.maxX() + 1;
        long maxY = routing.maxY() + 1;
        routing.updateBoundary(maxX, maxY);
    }
    
    rule +!askForTask() : leader(string leaderName) & waitingForTask(false) {
        -+waitingForTask(true);
        send(request, leaderName, requestTask());
    }
    
    rule +!askForTask() {
    }
    
    rule @message(inform, string sender, assignTask(long X, long Y)) {
        -+waitingForTask(false);
        +targetX(X);
        +targetY(Y);
    }
    
    rule @message(inform, string sender, noTask()) {
        -+waitingForTask(false);
    }
    
    rule +!cleanTask(long taskX, long taskY) : ei.square("here", "dust") & leader(string leaderName) {
        ei.clean();
        -targetX(taskX);
        -targetY(taskY);
        send(inform, leaderName, taskComplete(taskX, taskY));
        !move();
    }
    
    rule +!cleanTask(long taskX, long taskY) : leader(string leaderName) {
        -targetX(taskX);
        -targetY(taskY);
        send(inform, leaderName, taskComplete(taskX, taskY));
        !move();
    }
    
    rule +!doTask() : ei.square("here", "dust") & ei.location(long X, long Y) & leader(string leaderName) {
        ei.clean();
        send(inform, leaderName, taskComplete(X, Y));
        if (targetX(long tX) & targetY(long tY) & tX == X & tY == Y) {
            -targetX(tX);
            -targetY(tY);
        }
        !move();
    }
    
    rule +!doTask() : ~targetX(long tX) {
        !askForTask();
        !move();
    }
    
    rule +!doTask() {
        !move();
    }
    
    rule +!move() : ei.square("forward", "dust") {
        ei.move("forward");
    }
    
    rule +!move() : ei.square("left", "dust") {
        ei.move("left");
    }
    
    rule +!move() : ei.square("right", "dust") {
        ei.move("right");
    }
    
    rule +!move() : ei.square("forward", "vac") & ei.square("left", "vac") & 
                    ei.square("right", "vac") {
        ei.move("back");
    }
    
    rule +!move() : ei.square("forward", "vac") & ei.square("left", "obstacle") & 
                    ei.square("right", "obstacle") {
        ei.move("back");
    }
    
    rule +!move() : ei.square("forward", "obstacle") & ei.square("left", "vac") & 
                    ei.square("right", "vac") {
        ei.move("back");
    }
    
    rule +!move() : ei.square("forward", "vac") & ei.square("left", "vac") {
        ei.move("right");
    }
    
    rule +!move() : ei.square("forward", "vac") & ei.square("left", "obstacle") {
        ei.move("right");
    }
    
    rule +!move() : ei.square("forward", "vac") & ei.square("right", "vac") {
        ei.move("left");
    }
    
    rule +!move() : ei.square("forward", "vac") & ei.square("right", "obstacle") {
        ei.move("left");
    }
    
    rule +!move() : ei.square("forward", "vac") {
        ei.move("left");
    }
    
    rule +!move() : ei.square("forward", "obstacle") & ei.square("left", "obstacle") & 
                    ei.square("right", "obstacle") {
        ei.move("back");
    }
    
    rule +!move() : ei.square("forward", "obstacle") & ei.square("left", "obstacle") {
        ei.move("right");
    }
    
    rule +!move() : ei.square("forward", "obstacle") & ei.square("right", "obstacle") {
        ei.move("left");
    }
    
    rule +!move() : ei.square("forward", "obstacle") {
        ei.move("left");
    }
    
    rule +!move() : ei.square("forward", "empty") {
        ei.move("forward");
    }
    
    rule +!move() : ei.square("left", "empty") {
        ei.move("left");
    }
    
    rule +!move() : ei.square("right", "empty") {
        ei.move("right");
    }
    
    rule +!move() {
        ei.move("forward");
    }
}